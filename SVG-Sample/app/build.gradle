apply plugin: 'com.android.application'
apply plugin: 'svg'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.megatronking:svg-plugin:1.1.0'
    }
}

svg {
    // 配置vector目录，可以配置多个(如果直接使用svg格式图片，可以不配置此项)
    vectorDirs = ["src/main/res_vector/drawable"]
    // 配置生成空shape文件目录
    shapeDir = "src/main/res_shape/drawable"
    // 配置生成java文件目录
    javaDir = "src/main/java/com/github/megatron/svg/sample/drawables"
    // 配置应用包名(R文件包名)
    packageName = "com.github.megatron.svg.sample"

    // 定义svg生成vector的配置，可以配置多个目录，方便图片分类
    svg2vector {
        svg_a {
            svgDir = "${rootDir}/svg_a"
            vectorDir = "src/main/res_svg/drawable"
            // 配置生成的vector尺寸，默认不设置是24
            height = 48
            width = 48
        }
        svg_b {
            svgDir = "${rootDir}/svg_b"
            vectorDir = "src/main/res_svg/drawable"
            height = 48
            width = 48
        }
    }

}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.github.megatron.svg.sample"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        // 为了开发时能引用vector文件方便预览，将存放vector文件目录添加到资源文件目录中
        debug {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res', 'src/main/res_vector', 'src/main/res_svg']
        }
        release {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res', "src/main/res_shape"]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.0'
    testCompile 'junit:junit:4.12'
    compile 'com.github.megatronking:svg-support:1.0.1'
}
